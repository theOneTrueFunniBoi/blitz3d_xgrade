; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Blitz3D: X-Grade Edition"
!define PRODUCT_NAME_VALID "Blitz3D X-Grade Edition"
!define PRODUCT_NAME_SHORT "Blitz3D X-Grade"
!define PRODUCT_VERSION "1.000"
!define PRODUCT_VERSION_VALID "1_000"
# These three must be integers
!define VERSIONMAJOR 1
!define VERSIONMINOR 0
!define DESCRIPTION "A fork of Blitz3D: SoLoud Edition that aims to upgrade Blitz3D for the better via bug fixes, new commands, etc."
!define PRODUCT_PUBLISHER "funniman.exe"
!define PRODUCT_WEB_SITE "https://github.com/theOneTrueFunniBoi/blitz3d_xgrade"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

!define PRIMARY_APPLICATION "XGrade-Launcher.exe"

# This is the size (in kB) of all the files copied into "Program Files"
!define INSTALLSIZE 7233

!define env_hkcu 'HKCU "Environment"'

;!define ZIPDLL

; MUI 1.67 compatible ------
!include "MUI.nsh"

RequestExecutionLevel admin
!ifdef ZIPDLL
	!include "zipdll.nsh"
!endif

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\classic-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\classic-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "LICENSE.txt"
; Components page
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "XGrade-Setup-v${PRODUCT_VERSION_VALID}.exe"
InstallDir "$PROGRAMFILES\Blitz3D X-Grade"
ShowInstDetails show
ShowUnInstDetails show

InstType "Full" IT_FULL
InstType "Minimal" IT_MIN

Section "Primary Components" SEC01
  SectionInstType ${IT_FULL} ${IT_MIN}
  SetOverwrite ifdiff
!ifdef ZIPDLL
  SetOutPath "$INSTDIR"
  File "${LOCALDIR}\main.zip"
  StrCpy $R0 "$INSTDIR\main.zip"
  ZipDLL::extractall "$R0" "$INSTDIR"
  Delete "$R0"
!else
  SetOutPath "$INSTDIR\bin"
  File /a /r /x blitzviewer bin\*.*
  ;File /r /x bin\blitzviewer.exe
  SetOutPath "$INSTDIR\cfg"
  File /a /r /x *.prefs cfg\*.*
  ;File /r /x cfg\blitzide.prefs
  SetOutPath "$INSTDIR\Games"
  File /a /r Games\*.*
  SetOutPath "$INSTDIR\help"
  File /a /r help\*.*
  SetOutPath "$INSTDIR\samples"
  File /a /r samples\*.*
  SetOutPath "$INSTDIR\tutorials"
  File /a /r tutorials\*.*
  SetOutPath "$INSTDIR\userlibs"
  File /a userlibs\UserLibs.txt
  SetOutPath "$INSTDIR"
  File /a versions.txt
  File /a XGrade-Launcher.exe
!endif
  WriteRegExpandStr ${env_hkcu} "blitzpath" "$INSTDIR"
  SendMessage ${HWND_BROADCAST} ${WM_WININICHANGE} 0 "STR:Environment" /TIMEOUT=5000
  
  # Registry information for add/remove programs
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "DisplayName" "${PRODUCT_PUBLISHER} - ${PRODUCT_NAME} - ${DESCRIPTION}"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "UninstallString" "$\"$INSTDIR\uninst.exe$\""
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "QuietUninstallString" "$\"$INSTDIR\uninst.exe$\" /S"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "InstallLocation" "$\"$INSTDIR$\""
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "DisplayIcon" "$\"$INSTDIR\logo.ico$\""
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "Publisher" "$\"${PRODUCT_PUBLISHER}$\""
  #WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "HelpLink" "$\"${HELPURL}$\""
  #WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "URLUpdateInfo" "$\"${UPDATEURL}$\""
  #WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "URLInfoAbout" "$\"${ABOUTURL}$\""
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "DisplayVersion" "$\"${PRODUCT_VERSION}$\""
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "VersionMajor" ${VERSIONMAJOR}
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "VersionMinor" ${VERSIONMINOR}
  # There is no option for modifying or repairing the install
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "NoModify" 1
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "NoRepair" 1
  # Set the INSTALLSIZE constant (!defined at the top of this script) so Add/Remove Programs can accurately report the size
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_PUBLISHER} ${PRODUCT_NAME}" "EstimatedSize" ${INSTALLSIZE}
SectionEnd

Section /o "Blitz Media Viewer" SEC02
  SectionInstType ${IT_FULL}
  AddSize 3656
  SetOutPath "$INSTDIR"
!ifdef ZIPDLL
  SetOutPath "$INSTDIR"
  File "${LOCALDIR}\blitzviewer.zip"
  StrCpy $R0 "$INSTDIR\blitzviewer.zip"
  ZipDLL::extractall "$R0" "$INSTDIR"
  Delete "$R0"
!else
  SetOutPath "$INSTDIR\bin"
  File /a bin\blitzviewer.exe
  SetOutPath "$INSTDIR\BlitzViewer"
  File /a /r BlitzViewer\*.*
  SetOutPath "$INSTDIR\Media"
  File /a /r Media\*.*
!endif
  WriteRegExpandStr ${env_hkcu} "blitzviewer" "installed"
  SendMessage ${HWND_BROADCAST} ${WM_WININICHANGE} 0 "STR:Environment" /TIMEOUT=5000
SectionEnd

Section -AdditionalIcons
  SetOutPath $INSTDIR
  ;WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateDirectory "$SMPROGRAMS\${PRODUCT_NAME_SHORT}"
  ;CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME_SHORT}\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  WriteIniStr "$SMPROGRAMS\${PRODUCT_NAME_SHORT}\${PRODUCT_NAME_SHORT} Github.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME_SHORT}\${PRODUCT_NAME_VALID}.lnk" "$INSTDIR\${PRIMARY_APPLICATION}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME_SHORT}\Uninstall ${PRODUCT_NAME_SHORT}.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC01} "The primary application components."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC02} "A tool for viewing various types of Blitz3D's supported media."
!insertmacro MUI_FUNCTION_DESCRIPTION_END


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) has been successfully removed from your computer. Some registry or path values may not properly update until you restart your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you absolutely sure you would like to remove $(^Name) and all it's related componenets and features?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  DeleteRegValue ${env_hkcu} "blitzviewer"

  ;Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"

  Delete "$SMPROGRAMS\${PRODUCT_NAME_SHORT}\Uninstall ${PRODUCT_NAME_SHORT}.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_NAME_SHORT}\${PRODUCT_NAME_SHORT} Github.url"
  Delete "$SMPROGRAMS\${PRODUCT_NAME_SHORT}\${PRODUCT_NAME_VALID}.lnk"

  Delete "$INSTDIR\*.*"

  RMDir /r "$INSTDIR\*"
  
  ;SetOutPath "$INSTDIR\.."
  
  RMDir /r "$SMPROGRAMS\${PRODUCT_NAME_SHORT}\"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  SetAutoClose true
SectionEnd